// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

Shader "Custom/UI/Glitch"
{
    Properties
    {
        [PerRendererData] _MainTex ("Sprite Texture", 2D) = "white" {}
        [HDR] _Color ("Tint", Color) = (1,1,1,1)

        _StencilComp ("Stencil Comparison", Float) = 8
        _Stencil ("Stencil ID", Float) = 0
        _StencilOp ("Stencil Operation", Float) = 0
        _StencilWriteMask ("Stencil Write Mask", Float) = 255
        _StencilReadMask ("Stencil Read Mask", Float) = 255

        [Enum(UnityEngine.Rendering.BlendMode)]
        _BlendSrc("Blend Src Factor", int) = 1

        [Enum(UnityEngine.Rendering.BlendMode)]
        _BlendDst("Blend Dst Factor", int) = 10

        _ColorMask ("Color Mask", Float) = 15

        [Toggle(UNITY_UI_ALPHACLIP)] _UseUIAlphaClip ("Use Alpha Clip", Float) = 0
    }

    SubShader
    {
        Tags
        {
            "Queue"="Transparent"
            "IgnoreProjector"="True"
            "RenderType"="Transparent"
            "PreviewType"="Plane"
            "CanUseSpriteAtlas"="True"
        }

        Stencil
        {
            Ref [_Stencil]
            Comp [_StencilComp]
            Pass [_StencilOp]
            ReadMask [_StencilReadMask]
            WriteMask [_StencilWriteMask]
        }

        Cull Off
        Lighting Off
        ZWrite Off
        ZTest [unity_GUIZTestMode]

        Blend SrcAlpha OneMinusSrcAlpha
        ColorMask [_ColorMask]

        Pass
        {
            Name "Default"
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #pragma target 2.0

            #include "UnityCG.cginc"
            #include "UnityUI.cginc"

            #pragma multi_compile_local _ UNITY_UI_CLIP_RECT
            #pragma multi_compile_local _ UNITY_UI_ALPHACLIP
            #pragma multi_compile ALPHA ADD Capture

            struct appdata_t
            {
                float4 vertex : POSITION;
                float4 color : COLOR;
                float2 texcoord : TEXCOORD0;
                UNITY_VERTEX_INPUT_INSTANCE_ID
            };

            struct v2f
            {
                float4 vertex : SV_POSITION;
                float4 color : COLOR;
                float2 texcoord : TEXCOORD0;
                float4 worldPosition : TEXCOORD1;
                half4 mask : TEXCOORD2;
                UNITY_VERTEX_OUTPUT_STEREO
            };

            sampler2D _MainTex;
            float4 _Color;
            fixed4 _TextureSampleAdd;
            float4 _ClipRect;
            float4 _MainTex_ST;
            float _UIMaskSoftnessX;
            float _UIMaskSoftnessY;

            float2 _MainTex_TexelSize;

            float2 _ScanLineJitter; // (displacement, threshold)
            float _JitterSize;
            float2 _VerticalJump; // (amount, time)
            float _HorizontalShake;
            float _ColorDriftAmount; // (amount, time)
            float _ColorDriftTime;
            
            float4 _ScanlineColor;
            float _ScanlineSize;
            float _ColorStrength;
            

            // seed x, yで指定されたランダムに見える値。0 から １で正規化されている
            float nrand(float x, float y)
            {
                return frac(sin(dot(float2(x, y), float2(12.9898, 78.233))) * 43758.5453);
            }

            v2f vert(appdata_t v)
            {
                v2f OUT;
                UNITY_SETUP_INSTANCE_ID(v);
                UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(OUT);
                float4 vPosition = UnityObjectToClipPos(v.vertex);
                OUT.worldPosition = v.vertex;
                OUT.vertex = vPosition;

                float2 pixelSize = vPosition.w;
                pixelSize /= float2(1, 1) * abs(mul((float2x2)UNITY_MATRIX_P, _ScreenParams.xy));

                float4 clampedRect = clamp(_ClipRect, -2e10, 2e10);
                float2 maskUV = (v.vertex.xy - clampedRect.xy) / (clampedRect.zw - clampedRect.xy);
                OUT.texcoord = TRANSFORM_TEX(v.texcoord.xy, _MainTex);
                OUT.mask = half4(v.vertex.xy * 2 - clampedRect.xy - clampedRect.zw,
                                 0.25 / (0.25 * half2(_UIMaskSoftnessX, _UIMaskSoftnessY) + abs(pixelSize.xy)));

                OUT.color = v.color * _Color;
                return OUT;
            }

            half4 frag(v2f IN): SV_Target
            {
                const float u = IN.texcoord.x;
                const float v = IN.texcoord.y;
                
                // Scan line jitter
                // jitterとは、ゆらぎの意味
                // -1 から 1の値を取るランダムな値を取得
                float jitter = nrand(floor(v * _JitterSize), _Time.x) * 2 - 1;
                // Step - 0か１を返す。第一引数の値が、第２引数以上かどうか。
                // _ScanLineJitter.yがabs(jitter)より小さかったら0、そうでないなら1を返す

                // _ScanLineJitter.xの値だけズレる
                // _ScanLineJitter.yはthreshold
                // jitterが-1なら左にずれ、1なら右にずれる。
                // ずれるかどうかの判断はstepで行っている。
                jitter *= step(_ScanLineJitter.y, abs(jitter)) * _ScanLineJitter.x;

                // Vertical jump
                float jump = lerp(v, frac(v + _VerticalJump.y), _VerticalJump.x);
              
                // Horizontal shake
                float shake = (nrand(_Time.x, 2) - 0.5) * _HorizontalShake;

                // Color drift
                // _ColorDriftTime = _Time.x * 500;
                float drift = sin(jump + _ColorDriftTime) * _ColorDriftAmount;

                float u1 = saturate(u + jitter + shake);
                float u2 = saturate(u + jitter + shake + drift);
                float vv = saturate(jump);
                half4 src1 = tex2D(_MainTex, frac(float2(u1, vv)));
                half4 src2 = tex2D(_MainTex, frac(float2(u2, vv)));
                half4 output = half4(src1.r, src2.g, src1.b, src1.a);

                #ifdef UNITY_UI_CLIP_RECT
                half2 m = saturate((_ClipRect.zw - _ClipRect.xy - abs(IN.mask.xy)) * IN.mask.zw);
                output.a *= m.x * m.y;
                #endif
                
                #ifdef UNITY_UI_ALPHACLIP
                clip (output.a - 0.001);
                #endif
                
                // 0 〜 1の値を取る波
                float scanline = sin(v * _ScreenParams.y / _ScanlineSize + _Time.x * 400) * 0.5 + 0.5;
                scanline *= output.a;
                output = lerp(output, output * _ScanlineColor, scanline);
                output.rgb *= _ColorStrength;
                return half4(output.r, output.g, output.b, output.a * IN.color.a);
            }
            
            ENDCG
        }
    }
}